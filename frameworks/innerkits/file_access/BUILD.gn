# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/filemanagement/user_file_service/filemanagement_aafwk.gni")

BASE_DIR = "//foundation/filemanagement/user_file_service"

config("ability_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "${BASE_DIR}/utils",
    "${aafwk_services_path}/common/include",
    "${aafwk_kits_path}/ability/native/include/continuation/distributed",
    "${aafwk_kits_path}/ability/native/include/continuation/kits",
    "${aafwk_kits_path}/appkit/native/app/include",
    "${aafwk_path}/frameworks/js/napi/aafwk/inner/napi_common",
    "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_base/include",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

config("ability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "${BASE_DIR}/utils",
    "${aafwk_kits_path}/appkit/native/ability_runtime/app",
    "${aafwk_kits_path}/appkit/native/app/include",
    "${aafwk_kits_path}/appkit/native/ability_runtime/context",
  ]
}

ohos_shared_library("file_extension_ability_kit") {
  include_dirs = []

  sources = [
    "src/file_ext_ability.cpp",
    "src/js_file_ext_ability.cpp",
  ]
  configs = [ ":ability_config" ]
  public_configs = [
    ":ability_public_config",
    "${aafwk_kits_path}/ability/ability_runtime:ability_context_public_config",
  ]

  deps = [
    "${aafwk_kits_path}/ability/native:abilitykit_native",
    "${aafwk_kits_path}/appkit:app_context",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:app_manager",
    "ability_runtime:runtime",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "ipc:ipc_core",
    "ipc_js:rpc",
    "permission_standard:libpermissionsdk_standard",
    "utils_base:utils",
  ]

  public_deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/arkui/napi:ace_napi",
  ]

  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}

ohos_shared_library("file_extension_ability_module") {
  sources = [ "src/file_ext_ability_module_loader.cpp" ]

  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  deps = [
    ":file_extension_ability_kit",
    "${aafwk_kits_path}/ability/native:abilitykit_native",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:runtime",
    "hiviewdfx_hilog_native:libhilog",
    "utils_base:utils",
  ]

  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}