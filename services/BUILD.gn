# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/filemanagement/user_file_service/filemanagement_aafwk.gni")
FMS_BASE_DIR = "//foundation/filemanagement/user_file_service/services"

group("fms") {
  deps = [
    ":external_file_manager_hap",
    ":fms_server",
    ":fms_service.cfg",
  ]
}

ohos_prebuilt_etc("fms_service.cfg") {
  source = "etc/fms_service.cfg"
  relative_install_dir = "init"
  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}

ohos_shared_library("fms_server") {
  subsystem_name = "filemanagement"
  part_name = "user_file_service"

  include_dirs = [
    "$FMS_BASE_DIR/include",
    "$FMS_BASE_DIR/src/client",
    "$FMS_BASE_DIR/src/server",
    "$FMS_BASE_DIR/src/fileoper",
    "$FMS_BASE_DIR/include/fileoper",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include/hilog",
    "//foundation/filemanagement/storage_service/services/storage_manager/include",
    "//foundation/filemanagement/storage_service/interfaces/innerkits/storage_manager/native",
    "//foundation/distributeddatamgr/relational_store/interfaces/inner_api/appdatafwk/include",
    "//foundation/distributeddatamgr/relational_store/interfaces/inner_api/rdb/include",
  ]

  sources = [
    "src/client/file_manager_proxy.cpp",
    "src/fileoper/file_info.cpp",
    "src/fileoper/media_file_oper.cpp",
    "src/fileoper/media_file_utils.cpp",
    "src/fileoper/oper_factory.cpp",
    "src/server/file_manager_service.cpp",
    "src/server/file_manager_service_stub.cpp",
  ]

  deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/media_library_helper:media_library",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/medialibrary_data_extension:medialibrary_data_extension",
  ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "data_share:datashare_common",
    "data_share:datashare_consumer",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
}

ohos_hap("external_file_manager_hap") {
  hap_profile = "file_extension_hap/entry/src/main/module.json"
  deps = [
    ":external_file_manager_js_assets",
    ":external_file_manager_resources",
  ]
  certificate_profile = "signature/fileextension.p7b"
  hap_name = "external_file_manager"
  subsystem_name = "filemanagement"
  part_name = "user_file_service"
  module_install_dir = "app/com.ohos.UserFile.ExternalFileManager"
}

ohos_js_assets("external_file_manager_js_assets") {
  hap_profile = "file_extension_hap/entry/src/main/module.json"
  ets2abc = true
  source_dir = "file_extension_hap/entry/src/main/ets"
}

ohos_app_scope("external_file_manager_app_profile") {
  app_profile = "file_extension_hap/AppScope/app.json"
  sources = [ "file_extension_hap/AppScope/resources" ]
}

ohos_resources("external_file_manager_resources") {
  sources = [ "file_extension_hap/entry/src/main/resources" ]
  deps = [ ":external_file_manager_app_profile" ]
  hap_profile = "file_extension_hap/entry/src/main/module.json"
}
