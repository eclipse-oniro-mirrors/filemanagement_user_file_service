# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/filemanagement/user_file_service/filemanagement_aafwk.gni")

part_name = "user_file_service"
subsystem_name = "filemanagement"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/picker"

copy_taihe_idl("copy_taihe") {
  sources = [
    "${user_file_service_path}/interfaces/kits/taihe/picker/idl/ohos.file.picker.taihe",
  ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.file.picker.ani.cpp",
    "$taihe_generated_file_path/src/ohos.file.picker.abi.c",
  ]
}

taihe_shared_library("file_picker_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${user_file_service_path}/interfaces/common/include",
    "${user_file_service_path}/interfaces/common/include/log.h",
    "${user_file_service_path}/interfaces/innerkits/native/picker/include",
    "${user_file_service_path}/interfaces/kits/taihe/picker/include",
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
   "src/ani_constructor.cpp",
   "src/ohos.file.picker.impl.cpp",
   "src/taihe_modal_ui_callback.cpp"
  ]

  deps = [
    ":run_taihe",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:base",
    "ability_base:zuri",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",    
    "ability_runtime:app_context",
    "ability_runtime:dataobs_manager",
    # "ability_runtime:napi_base_context",
    # "ability_runtime:napi_common",
    "ability_runtime:runtime",
    "ability_runtime:service_extension",
    "ability_runtime:ui_extension",
    "ace_engine:ace_uicontent",
    "file_api:filemgmt_libhilog",
    "file_api:filemgmt_libn",
    "ipc:ipc_napi",
    "ipc:ipc_single",
    "napi:ace_napi",
    "window_manager:libwm",
    "hilog:libhilog",    
    "c_utils:utils",
    "window_manager:windowstageani_kit",
    "window_manager:window_native_kit",
    "ability_runtime:ani_base_context",
    "udmf:udmf_client",
  ]
}

generate_static_abc("file_picker_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.file.picker.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/file_picker_taihe_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("file_picker_etc") {
  source = "$target_out_dir/file_picker_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "user_file_service"
  subsystem_name = "filemanagement"
  deps = [
    ":file_picker_taihe_abc",
  ]
}

group("afs_picker_taihe") {
  deps = [
    ":file_picker_taihe",
    ":file_picker_etc",
  ]
}
